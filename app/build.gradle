apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'


Properties properties = new Properties()
properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
def AAVersion = properties.getProperty("AAVersion") as String
def buildTools = properties.getProperty("BuildToolsVersion") as String
def appName = properties.getProperty("AppName") as String
def signFile = properties.getProperty("storeFile") as String
def signStorePass = properties.getProperty("storePassword") as String
def signAlias = properties.getProperty("keyAlias") as String
def signKeyPass = properties.getProperty("keyPassword") as String
def appId = properties.getProperty("AppId") as String

System.out.print(
        String.format("\nBUILD CONFIGURATION : \n\nAndroidAnnotations version: %s\nBuildToolsVersion : %s\nAppName: %s\nSigningFile: %s\nAppId: %s\n\nBUILD CONFIG END\n\n",
                AAVersion, buildTools, appName, signFile, appId));

android {
    compileSdkVersion 23
    buildToolsVersion buildTools
    defaultConfig {
        applicationId appId
        minSdkVersion 17
        targetSdkVersion 23
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName System.getenv("VERSION_NAME") as String ?: "0.0.1"
        project.archivesBaseName = appName + "-" + versionName
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    signingConfigs {
        release {
            storeFile file(signFile)
            storePassword signStorePass
            keyAlias signAlias
            keyPassword signKeyPass
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.0@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:core:0.8.5.0@aar') {
        transitive = true
    }
    compile files('libs/feedback_v6.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.orhanobut:logger:1.9'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'ch.acra:acra:4.6.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.2'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.google.guava:guava:19.0-rc2'
    compile 'com.jonathanfinerty.once:once:0.4.0'
    compile 'com.akexorcist:RoundCornerProgressBar:2.0.3'
    compile 'net.danlew:android.joda:2.8.1'
    compile 'com.github.jorgecastilloprz:fabprogresscircle:1.01@aar'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName appId
    }
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
